
buildscript {
    repositories {
        mavenLocal()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

plugins {
    id 'net.researchgate.release' version '2.8.0'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'propdeps'
apply plugin: 'propdeps-maven'
apply plugin: 'propdeps-idea'

configurations {
    source {
        extendsFrom archives
    }
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    def CUCUMBER_VERSION = '1.2.2'

    compile gradleApi()
    compile localGroovy()
    implementation group: 'commons-lang', name: 'commons-lang', version: '2.6'

    provided 'info.cukes:cucumber-core:' + CUCUMBER_VERSION

    testCompile 'junit:junit:4.11'
    testCompile 'info.cukes:cucumber-junit:' + CUCUMBER_VERSION
    testCompile 'info.cukes:cucumber-java:' + CUCUMBER_VERSION
    testCompile 'commons-io:commons-io:2.4'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

task jarSources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task jarJavadoc(type: Jar, dependsOn: 'javadoc') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jarSources
    archives jarJavadoc
}


release {
    project.ext.set("release.useAutomaticVersion", true)
    git {
        requireBranch = ''
    }
    failOnCommitNeeded = false
    failOnPublishNeeded = false
    failOnUnversionedFiles = false
    buildTasks = []
}

task createWrapper(type: Wrapper) {
    gradleVersion = '5.0'
}

task printMainRuntimeClasspath {
    description 'Prints the runtime classpath of the built artifact (used by Cucumber acceptance tests)'
    doLast {
        sourceSets.main.runtimeClasspath.each { println it }
    }
}